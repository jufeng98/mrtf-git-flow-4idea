{
  parserClass="com.github.xiaolyuh.http.parser.HttpParser"
  parserUtilClass="com.github.xiaolyuh.http.parser.HttpParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Http"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xiaolyuh.http.psi"
  psiImplPackage="com.github.xiaolyuh.http.psi.impl"

  elementTypeHolderClass="com.github.xiaolyuh.http.psi.HttpTypes"
  elementTypeClass="com.github.xiaolyuh.http.psi.HttpElementType"
  tokenTypeClass="com.github.xiaolyuh.http.psi.HttpTokenType"
  psiImplUtilClass="com.github.xiaolyuh.http.psi.impl.HttpPsiImplUtil"

  tokens=[
    T_LT='<'
    T_RT='>'
    T_RT_DBL='>>'
    PATH='regexp:(([A-E]:)|[a-z./])([\u4E00-\u9FA5\w\-./\\]+/?)+'
    URL_DESC='regexp:(https?|wss?|\{\{)[-\w+&${}()#/%?=~|!:,.;]*[-\w+,&${}()#/%=~| ]'
    HEADER_DESC='regexp:[a-zA-Z\-]+:[\u4E00-\u9FA5\w,${}()=;%\\".\-*:/ ]+'
    REQUEST_COMMENT='regexp:#.*'
    LINE_COMMENT='regexp://.*'
    VARIABLE_DEFINE='regexp:@\w+[\w{}=]+'
    URL_FORM_ENCODE="regexp:[\w,&$={}():]*"
    JSON_TEXT="regexp:\{}|\[]|\{[\s\"][^#]*}|\[[\s\d\"a-z{][^#]*]"
    XML_TEXT="regexp:<[!\w][^â™£]*\w>"
    MULTIPART_SEPERATE='regexp:--[\w\-]+'
    JS_SCRIPT='regexp:\{%[^#]*%}'
    SPACE='regexp:\s+'
  ]
}
httpFile ::= (request | globalVariableDefinition | globalScript)*
request ::= method url version? headers? body? script? output_file? {
    pin = 1
}
url ::= URL_DESC {
    methods = [
        getReferences
    ]
}
version ::= 'HTTP/1.1' | 'HTTP/2.0'
method ::= GET | POST | DELETE | PUT
headers ::= header*
header ::= HEADER_DESC
body ::= ordinary_content | multipart_content
script ::= T_RT JS_SCRIPT {
    mixin = "com.github.xiaolyuh.http.inject.HttpPsiLanguageInjectionHost"
}
globalScript ::= script
globalVariableDefinition ::= VARIABLE_DEFINE
ordinary_content ::= file | URL_FORM_ENCODE | JSON_TEXT | XML_TEXT | URL_DESC {
    mixin = "com.github.xiaolyuh.http.inject.HttpPsiLanguageInjectionHost"
}
file ::= T_LT file_path
output_file ::= T_RT_DBL file_path
file_path ::= PATH
multipart_content ::= multipart_body* MULTIPART_SEPERATE
multipart_body ::= MULTIPART_SEPERATE header* ordinary_content